plugins {
    id 'groovy'
    id 'java-gradle-plugin'
    id 'maven-publish'
    id "com.gradle.plugin-publish" version "1.1.0"
}

group = 'com.kazurayam'
version = '0.2.0-SNAPSHOT'

java {
    withSourcesJar()
    withJavadocJar()
}

repositories {
    mavenCentral()
}

def functionalTest = sourceSets.create("functionalTest")
def functionalTestTask = tasks.register("functionalTest", Test) {
    description = "Runs the functional tests."
    group = "verification"
    testClassesDirs = functionalTest.output.classesDirs
    classpath = functionalTest.runtimeClasspath
    mustRunAfter(tasks.named('test'))
}

tasks.named('check') {
    dependsOn(functionalTestTask)
}

dependencies {
    implementation gradleApi()
    implementation localGroovy()
    testImplementation libs.junit.api
    testRuntimeOnly libs.junit.engine
    functionalTestImplementation platform(libs.spock.bom)
    functionalTestImplementation libs.spock.core
}

tasks.withType(Test) {
    useJUnitPlatform()
}


gradlePlugin {
    // configure the `java-gradle-plugin` so that it looks at the `sourceSets.functionalTest`
    // to find the tests for the custom plugin.
    testSourceSets(sourceSets.functionalTest)
    // This makes `org.gradle.testkit.runner.GradleRunner` class available to the
    // functionalTest classes.

    plugins {
        inspectus4katalonPlugin {
            id = "com.kazurayam.inspectus4katalon"
            displayName = "Inspectus for Katalon Gradle Plugin"
            description = "adds 'gradle drivers' task which downloads required jars" +
                    " from the Maven Central repository to locate them into" +
                    " the Drivers folder of a Katalon Studio project"
            implementationClass = "com.kazurayam.inspectus.gradle.KatalonDriversPlugin"
        }
    }
}

pluginBundle {
    // information displayed in the Gradle Plugin Portal
    website = "https://github.com/kazurayam/inspectus4katalon-gradle-plugin"
    vcsUrl = "https://github.com/kazurayam/inspectus4katalon-gradle-plugin"
    tags = ["inspectus", "visual-inspection", "katalon-studio"]
}
