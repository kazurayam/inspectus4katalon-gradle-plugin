plugins {
    id 'groovy'
    id "com.gradle.plugin-publish" version "1.1.0"
}

group = 'com.kazurayam'
version = '0.3.2'

ext {
    isReleaseVersion = !version.endsWith("SNAPSHOT")
}

repositories {
    mavenCentral()
}

java {
    withSourcesJar()
    withJavadocJar()
}

tasks.withType(Test) {
    useJUnitPlatform()
}

def functionalTest = sourceSets.create("functionalTest")
def functionalTestTask = tasks.register("functionalTest", Test) {
    description = "Runs the functional tests."
    group = "verification"
    testClassesDirs = functionalTest.output.classesDirs
    classpath = functionalTest.runtimeClasspath
    mustRunAfter(tasks.named('test'))
}
tasks.named('check') {
    dependsOn(test, functionalTestTask)
}

dependencies {
    implementation gradleApi()
    implementation localGroovy()
    testImplementation libs.junit.api
    testRuntimeOnly libs.junit.engine
    functionalTestImplementation platform(libs.spock.bom)
    functionalTestImplementation libs.spock.core
}

gradlePlugin {
    // configure the `java-gradle-plugin` so that it looks at the `sourceSets.functionalTest`
    // to find the tests for the custom plugin.
    testSourceSets(sourceSets.functionalTest)
    // This makes `org.gradle.testkit.runner.GradleRunner` class available to the
    // functionalTest classes.

    plugins {
        inspectus4katalon {
            id = "com.kazurayam.inspectus4katalon"
            displayName = "Inspectus for Katalon Gradle Plugin"
            description = "adds 'gradle drivers' task which downloads required jars" +
                    " from the Maven Central repository to locate them into" +
                    " the Drivers folder of a Katalon Studio project"
            implementationClass = "com.kazurayam.inspectus.gradle.KatalonDriversPlugin"
        }
    }
}

pluginBundle {
    // information displayed in the Gradle Plugin Portal
    website = 'https://kazurayam.github.io/inspectus4katalon-gradle-plugin/'
    vcsUrl = 'https://github.com/kazurayam/inspectus4katalon-gradle-plugin/'
    tags = ["inspectus", "visual-inspection", "katalon-studio", "materialstore"]
}

publishing {
    repositories {
        maven {
            name = "gpr"
            url = uri("https://maven.pkg.github.com/kazurayam/inspectus4katalon-gradle-plugin")
            credentials {
                username = project.findProperty("gpr.user")
                password = project.findProperty("gpr.key")
            }
        }
    }
}

